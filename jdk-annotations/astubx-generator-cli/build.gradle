plugins {
    id "java-library"
    id "com.gradleup.shadow"
}

jar{
    manifest {
        attributes('Main-Class':'com.uber.nullaway.jdkannotations.AstubxGeneratorCLI')
    }
    // add this classifier so that the output file for the jar task differs from
    // the output file for the shadowJar task (otherwise they overwrite each other's
    // outputs, forcing the tasks to always re-run)
    archiveClassifier = "nonshadow"
}

shadowJar {
    mergeServiceFiles()
    configurations = [
        project.configurations.runtimeClasspath
    ]
    archiveClassifier = ""
}
shadowJar.dependsOn jar
assemble.dependsOn shadowJar

dependencies {
    implementation project(":library-model:library-model-generator")

    implementation deps.build.guava
    implementation deps.build.javaparser
    implementation deps.build.javaparserSymbolSolver
    compileOnly deps.apt.autoValueAnnot
    annotationProcessor deps.apt.autoValue

    testImplementation deps.test.junit4

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
}
